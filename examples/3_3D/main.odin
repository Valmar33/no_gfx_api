
package main

import log "core:log"
import "../../gpu"
import "core:math"
import "core:math/linalg"
import "core:fmt"
import intr "base:intrinsics"

import sdl "vendor:sdl3"

import shared "../shared"
import gltf2 "../shared/gltf2"

Start_Window_Size_X :: 1000
Start_Window_Size_Y :: 1000
Frames_In_Flight :: 3
Example_Name :: "3D"

Sponza_Scene :: #load("../shared/assets/sponza.glb")

main :: proc()
{
    fmt.println("Right-click + WASD for first-person controls.")

    ok_i := sdl.Init({ .VIDEO })
    assert(ok_i)

    console_logger := log.create_console_logger()
    defer log.destroy_console_logger(console_logger)
    context.logger = console_logger

    ts_freq := sdl.GetPerformanceFrequency()
    max_delta_time: f32 = 1.0 / 10.0  // 10fps

    window_flags :: sdl.WindowFlags {
        .HIGH_PIXEL_DENSITY,
        .VULKAN,
        .RESIZABLE,
    }
    window := sdl.CreateWindow(Example_Name, Start_Window_Size_X, Start_Window_Size_Y, window_flags)
    ensure(window != nil)

    window_size_x := i32(Start_Window_Size_X)
    window_size_y := i32(Start_Window_Size_Y)

    gpu.init()
    defer gpu.cleanup()

    gpu.swapchain_init_from_sdl(window, Frames_In_Flight)

    depth_desc := gpu.Texture_Desc {
        dimensions = { u32(window_size_x), u32(window_size_y), 1 },
        format = .D32_Float,
        mip_count = 1,
        layer_count = 1,
        sample_count = 1,
        usage = { .Depth_Stencil_Attachment },
    }
    depth_texture := gpu.texture_alloc_and_create(depth_desc)
    defer gpu.texture_free_and_destroy(&depth_texture)

    vert_shader := gpu.shader_create(#load("shaders/test.vert.spv", []u32), .Vertex)
    frag_shader := gpu.shader_create(#load("shaders/test.frag.spv", []u32), .Fragment)
    defer {
        gpu.shader_destroy(vert_shader)
        gpu.shader_destroy(frag_shader)
    }

    upload_arena := gpu.arena_init(1024 * 1024 * 1024)
    defer gpu.arena_destroy(&upload_arena)

    scene, _, gltf_data := shared.load_scene_gltf(Sponza_Scene)
    defer {
        shared.destroy_scene(&scene)
        gltf2.unload(gltf_data)
    }

    meshes_gpu: [dynamic]Mesh_GPU
    defer {
        for &mesh_gpu in meshes_gpu do mesh_destroy(&mesh_gpu)
        delete(meshes_gpu)
    }

    upload_cmd_buf := gpu.commands_begin(.Main)
    for mesh in scene.meshes {
        append(&meshes_gpu, upload_mesh(&upload_arena, upload_cmd_buf, mesh))
    }
    gpu.cmd_barrier(upload_cmd_buf, .Transfer, .All, {})
    gpu.queue_submit(.Main, { upload_cmd_buf })

    now_ts := sdl.GetPerformanceCounter()

    frame_arenas: [Frames_In_Flight]gpu.Arena
    for &frame_arena in frame_arenas do frame_arena = gpu.arena_init(10 * 1024 * 1024)
    defer for &frame_arena in frame_arenas do gpu.arena_destroy(&frame_arena)
    next_frame := u64(1)
    frame_sem := gpu.semaphore_create(0)
    defer gpu.semaphore_destroy(&frame_sem)
    for true
    {
        proceed := shared.handle_window_events(window)
        if !proceed do break

        old_window_size_x := window_size_x
        old_window_size_y := window_size_y
        sdl.GetWindowSize(window, &window_size_x, &window_size_y)
        if .MINIMIZED in sdl.GetWindowFlags(window) || window_size_x <= 0 || window_size_y <= 0
        {
            sdl.Delay(16)
            continue
        }

        if next_frame > Frames_In_Flight {
            gpu.semaphore_wait(frame_sem, next_frame - Frames_In_Flight)
        }
        if old_window_size_x != window_size_x || old_window_size_y != window_size_y
        {
            gpu.queue_wait_idle(.Main)
            gpu.swapchain_resize({ u32(max(0, window_size_x)), u32(max(0, window_size_y)) })
            depth_desc.dimensions.x = u32(window_size_x)
            depth_desc.dimensions.y = u32(window_size_y)
            gpu.texture_free_and_destroy(&depth_texture)
            depth_texture = gpu.texture_alloc_and_create(depth_desc)
        }

        swapchain := gpu.swapchain_acquire_next()  // Blocks CPU until at least one frame is available.

        frame_arena := &frame_arenas[next_frame % Frames_In_Flight]
        gpu.arena_free_all(frame_arena)

        last_ts := now_ts
        now_ts = sdl.GetPerformanceCounter()
        delta_time := min(max_delta_time, f32(f64((now_ts - last_ts)*1000) / f64(ts_freq)) / 1000.0)

        world_to_view := shared.first_person_camera_view(delta_time)
        aspect_ratio := f32(window_size_x) / f32(window_size_y)
        view_to_proj := linalg.matrix4_perspective_f32(math.RAD_PER_DEG * 59.0, aspect_ratio, 0.1, 1000.0, false)

        cmd_buf := gpu.commands_begin(.Main)
        gpu.cmd_begin_render_pass(cmd_buf, {
            color_attachments = {
                { texture = swapchain, clear_color = { 0.7, 0.7, 0.7, 1.0 } }
            },
            depth_attachment = gpu.Render_Attachment {
                texture = depth_texture, clear_color = 1.0
            },
        })
        gpu.cmd_set_shaders(cmd_buf, vert_shader, frag_shader)

        gpu.cmd_set_depth_state(cmd_buf, { mode = { .Read, .Write }, compare = .Less })

        for instance in scene.instances
        {
            mesh := meshes_gpu[instance.mesh_idx]

            Vert_Data :: struct #all_or_none {
                positions: rawptr,
                normals: rawptr,
                model_to_world: [16]f32,
                model_to_world_normal: [16]f32,
                world_to_view: [16]f32,
                view_to_proj: [16]f32,
            }
            #assert(size_of(Vert_Data) == 8+8+64+64+64+64)
            verts_data := gpu.arena_alloc(frame_arena, Vert_Data)
            verts_data.cpu^ = {
                positions = mesh.pos.gpu.ptr,
                normals = mesh.normals.gpu.ptr,
                model_to_world = intr.matrix_flatten(instance.transform),
                model_to_world_normal = intr.matrix_flatten(linalg.transpose(linalg.inverse(instance.transform))),
                world_to_view = intr.matrix_flatten(world_to_view),
                view_to_proj = intr.matrix_flatten(view_to_proj),
            }

            gpu.cmd_draw_indexed_instanced(cmd_buf, verts_data.gpu, {}, mesh.indices, mesh.idx_count, 1)
        }

        gpu.cmd_end_render_pass(cmd_buf)
        gpu.queue_submit(.Main, { cmd_buf }, frame_sem, next_frame)

        gpu.swapchain_present(.Main, frame_sem, next_frame)
        next_frame += 1
    }

    gpu.wait_idle()
}

Mesh_GPU :: struct
{
    pos: gpu.slice_t([4]f32),
    normals: gpu.slice_t([4]f32),
    uvs: gpu.slice_t([2]f32),
    indices: gpu.slice_t(u32),
    idx_count: u32,
}

upload_mesh :: proc(upload_arena: ^gpu.Arena, cmd_buf: gpu.Command_Buffer, mesh: shared.Mesh) -> Mesh_GPU
{
    assert(len(mesh.pos) == len(mesh.normals))
    assert(len(mesh.pos) == len(mesh.uvs))

    positions_staging := gpu.arena_alloc(upload_arena, [4]f32, len(mesh.pos))
    normals_staging := gpu.arena_alloc(upload_arena, [4]f32, len(mesh.normals))
    uvs_staging := gpu.arena_alloc(upload_arena, [2]f32, len(mesh.uvs))
    indices_staging := gpu.arena_alloc(upload_arena, u32, len(mesh.indices))
    copy(positions_staging.cpu, mesh.pos[:])
    copy(normals_staging.cpu, mesh.normals[:])
    copy(uvs_staging.cpu, mesh.uvs[:])
    copy(indices_staging.cpu, mesh.indices[:])

    res: Mesh_GPU
    res.pos = gpu.mem_alloc([4]f32, len(mesh.pos), gpu.Memory.GPU)
    res.normals = gpu.mem_alloc([4]f32, len(mesh.normals), gpu.Memory.GPU)
    res.uvs = gpu.mem_alloc([2]f32, len(mesh.uvs), gpu.Memory.GPU)
    res.indices = gpu.mem_alloc(u32, len(mesh.indices), gpu.Memory.GPU)
    res.idx_count = u32(len(mesh.indices))
    gpu.cmd_mem_copy(cmd_buf, res.pos, positions_staging, len(mesh.pos))
    gpu.cmd_mem_copy(cmd_buf, res.normals, normals_staging, len(mesh.normals))
    gpu.cmd_mem_copy(cmd_buf, res.uvs, uvs_staging, len(mesh.uvs))
    gpu.cmd_mem_copy(cmd_buf, res.indices, indices_staging, len(mesh.indices))
    return res
}

mesh_destroy :: proc(mesh: ^Mesh_GPU)
{
    gpu.mem_free(mesh.pos)
    gpu.mem_free(mesh.normals)
    gpu.mem_free(mesh.uvs)
    gpu.mem_free(mesh.indices)
    mesh^ = {}
}
