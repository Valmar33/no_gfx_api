
#+feature dynamic-literals
#+feature using-stmt

package main

import "base:runtime"
import "core:slice"
import intr "base:intrinsics"
import str "core:strings"

Lang_Feature :: enum { Raytracing }
Lang_Features :: bit_set[Lang_Feature; u32]

Any_Node :: union
{
    Any_Statement,
    Any_Expr,
    ^Ast_Decl,
}

Ast :: struct
{
    used_types: [dynamic]Ast_Type,
    used_in_locations: map[u32]Ast_Type,
    used_out_locations: map[u32]Ast_Type,
    used_data_type: ^Ast_Type,
    used_indirect_data_type: ^Ast_Type,
    scope: ^Ast_Scope,
    procs: [dynamic]^Ast_Proc_Def,
    global_vars: [dynamic]^Ast_Define_Var,

    // Filled in by typechecker
    used_features: Lang_Features,
}

Ast_Node :: struct
{
    token: Token,
    derived: Any_Node,
}

Ast_Scope :: struct
{
    enclosing_scope: ^Ast_Scope,  // Can be nil
    decls: [dynamic]^Ast_Decl,
}

// Declarations (global)

Ast_Decl :: struct
{
    using base: Ast_Node,
    name: string,
    type: ^Ast_Type,
    attr: Maybe(Ast_Attribute),
}

Ast_Proc_Def :: struct
{
    decl: ^Ast_Decl,
    statements: []^Ast_Statement,
    scope: ^Ast_Scope,
}

// Expressions

Any_Expr :: union
{
    ^Ast_Binary_Expr,
    ^Ast_Unary_Expr,
    ^Ast_Member_Access,
    ^Ast_Array_Access,
    ^Ast_Ident_Expr,
    ^Ast_Lit_Expr,
    ^Ast_Call,
}

Ast_Expr :: struct
{
    using base: Ast_Node,
    derived_expr: Any_Expr,
    type: ^Ast_Type,
}

Ast_Attribute_Type :: enum
{
    Vert_ID,
    Position,
    Data,
    Instance_ID,
    Draw_ID,
    Indirect_Data,
    Workgroup_ID,
    Local_Invocation_ID,
    Global_Invocation_ID,
    Group_Size,

    // With args:
    Out_Loc,
    In_Loc,
}

Ast_Attribute :: struct
{
    type: Ast_Attribute_Type,
    arg: u32
}

Ast_Binary_Op :: enum
{
    Add,
    Minus,
    Mul,
    Div,

    // Bitwise
    Bitwise_And,
    Bitwise_Or,
    Bitwise_Xor,
    LShift,
    RShift,

    And,
    Or,

    // Comparison
    Greater,
    Less,
    LE,
    GE,
    EQ,
    NEQ,
}

Ast_Binary_Expr :: struct
{
    using base_expr: Ast_Expr,
    lhs: ^Ast_Expr,
    rhs: ^Ast_Expr,
    op: Ast_Binary_Op,
}

Ast_Unary_Op :: enum
{
    Not,
    Plus,
    Minus,
}

Ast_Unary_Expr :: struct
{
    using base_expr: Ast_Expr,
    op: Ast_Unary_Op,
    expr: ^Ast_Expr,
}

Ast_Member_Access :: struct
{
    using base_expr: Ast_Expr,
    target: ^Ast_Expr,
    member_name: string,
}

Ast_Array_Access :: struct
{
    using base_expr: Ast_Expr,
    target: ^Ast_Expr,
    idx_expr: ^Ast_Expr,
}

Ast_Call :: struct
{
    using base_expr: Ast_Expr,
    target: ^Ast_Expr,
    args: []^Ast_Expr,
}

Ast_Ident_Expr :: struct
{
    using base_expr: Ast_Expr
}

Ast_Lit_Expr :: struct
{
    using base_expr: Ast_Expr
}

// Statements

Any_Statement :: union
{
    ^Ast_Assign,
    ^Ast_Stmt_Expr,
    ^Ast_Define_Var,
    ^Ast_Return,
    ^Ast_If,
    ^Ast_For,
    ^Ast_Break,
    ^Ast_Continue,
}

Ast_Statement :: struct
{
    using base: Ast_Node,
    derived_statement: Any_Statement,
}

Ast_Assign :: struct
{
    using base_statement: Ast_Statement,
    lhs: ^Ast_Expr,
    rhs: ^Ast_Expr
}

Ast_Define_Var :: struct
{
    using base_statement: Ast_Statement,
    decl: ^Ast_Decl,
    expr: ^Ast_Expr,
}

Ast_Stmt_Expr :: struct
{
    using base_statement: Ast_Statement,
    expr: ^Ast_Expr,
}

Ast_Return :: struct
{
    using base_statement: Ast_Statement,
    expr: ^Ast_Expr,
}

Ast_If :: struct
{
    using base_statement: Ast_Statement,
    cond: ^Ast_Expr,

    // Then
    statements: []^Ast_Statement,
    scope: ^Ast_Scope,

    // Else
    else_is_present: bool,
    else_is_single: bool,
    else_single: ^Ast_Statement,
    else_scope: ^Ast_Scope,
    else_multi_statements: []^Ast_Statement,
}

Ast_For :: struct
{
    using base_statement: Ast_Statement,
    define: ^Ast_Define_Var,
    cond: ^Ast_Expr,
    iter: ^Ast_Assign,

    statements: []^Ast_Statement,
    scope: ^Ast_Scope,
}

Ast_Break :: struct { using base_statement: Ast_Statement }
Ast_Continue :: struct { using base_statement: Ast_Statement }

// Types

Ast_Type_Kind :: enum
{
    Poison = 0,
    None,  // "void" for returnless functions
    Unknown,  // For typeless initialization
    Label,
    Pointer,
    Slice,
    Proc,
    Primitive,
    Struct,
}

Ast_Type_Primitive_Kind :: enum
{
    None = 0,
    Untyped_Int,
    Untyped_Float,
    Bool,
    Float,
    Uint,
    Int,
    Texture_ID,
    Sampler_ID,
    Vec2,
    Vec3,
    Vec4,
    Mat4,

    Ray_Query,
    BVH_ID,
}

Ast_Type :: struct
{
    kind: Ast_Type_Kind,
    primitive_kind: Ast_Type_Primitive_Kind,  // Only populated if kind == .Primitive
    base: ^Ast_Type,

    name: Token,

    // Proc
    args: []^Ast_Decl,
    ret: ^Ast_Type,
    ret_attr: Maybe(Ast_Attribute),

    // Struct
    members: []^Ast_Decl,
}

parse_file :: proc(filename: string, tokens: []Token, allocator: runtime.Allocator) -> (Ast, bool)
{
    context.allocator = allocator

    parser := Parser {
        tokens = tokens,
        filename = filename,
    }
    ast := _parse_file(&parser)
    return ast, !parser.error
}

Parser :: struct
{
    tokens: []Token,
    filename: string,
    at: u32,
    error: bool,
    scope: ^Ast_Scope,
    used_types: [dynamic]Ast_Type,
    used_out_locations: map[u32]Ast_Type,
    used_in_locations: map[u32]Ast_Type,
    used_data_type: ^Ast_Type,
    used_indirect_data_type: ^Ast_Type,
}

_parse_file :: proc(using p: ^Parser) -> Ast
{
    ast := Ast {
        scope = new(Ast_Scope)
    }

    scope = ast.scope

    loop: for true
    {
        #partial switch tokens[at].type
        {
            case .Ident:
            {
                if tokens[at+1].type == .Colon &&
                   tokens[at+2].type == .Colon &&
                   tokens[at+3].type == .Struct
                {
                    parse_struct_def(p)
                }
                else if tokens[at+1].type == .Colon &&
                        tokens[at+2].type == .Colon &&
                        tokens[at+3].type == .LParen
                {
                    append(&ast.procs, parse_proc_def(p))
                }
                else if tokens[at+1].type == .Colon
                {
                    ident := tokens[at].text
                    at += 2

                    decl := make_node(p, Ast_Decl)
                    decl.name = ident
                    append(&scope.decls, decl)

                    type: ^Ast_Type
                    if tokens[at].type == .Assign
                    {
                        type = new(Ast_Type)
                        type.kind = .Unknown
                    }
                    else
                    {
                        type = parse_type(p)
                    }
                    decl.type = type

                    if optional_token(p, .Assign)
                    {
                        def_var := make_statement(p, Ast_Define_Var)
                        def_var.decl = decl
                        def_var.expr = parse_expr(p)
                        append(&ast.global_vars, def_var)
                    }

                    required_token(p, .Semi)
                }
                else
                {
                    parse_error(p, "Expecting struct, global variable of procedure at top level.")
                    break loop
                }
            }
            case .EOS: break loop
            case:
            {
                parse_error(p, "Expecting an identifier at top level.")
                break loop
            }
        }
    }

    ast.used_types = used_types
    ast.used_out_locations = used_out_locations
    ast.used_in_locations = used_in_locations
    ast.used_data_type = used_data_type
    ast.used_indirect_data_type = used_indirect_data_type
    return ast
}

parse_struct_def :: proc(using p: ^Parser) -> ^Ast_Decl
{
    node := make_node(p, Ast_Decl)
    append(&scope.decls, node)

    struct_type := new(Ast_Type)
    struct_type.kind = .Struct
    node.type = struct_type

    ident := required_token(p, .Ident)
    node.name = ident.text

    required_token(p, .Colon)
    required_token(p, .Colon)
    required_token(p, .Struct)
    required_token(p, .LBrace)
    struct_type.members = parse_decl_list(p, false)
    required_token(p, .RBrace)
    return node
}

parse_proc_def :: proc(using p: ^Parser) -> ^Ast_Proc_Def
{
    decl := make_node(p, Ast_Decl)
    append(&scope.decls, decl)

    proc_type := new(Ast_Type)
    proc_type.kind = .Proc
    decl.type = proc_type

    old_scope := scope
    scope = new(Ast_Scope)
    scope.enclosing_scope = old_scope
    defer scope = old_scope

    proc_def := new(Ast_Proc_Def)
    proc_def.decl = decl
    proc_def.scope = scope

    ident := required_token(p, .Ident)
    decl.name = ident.text

    required_token(p, .Colon)
    required_token(p, .Colon)
    required_token(p, .LParen)
    proc_type.args = parse_decl_list(p, true)
    required_token(p, .RParen)

    if optional_token(p, .Arrow)
    {
        proc_type.ret = parse_type(p)
        if tokens[at].type == .Attribute {
            proc_type.ret_attr = parse_attribute(p)
            if proc_type.ret_attr != nil
            {
                if proc_type.ret_attr.?.type == .In_Loc {
                    used_in_locations[proc_type.ret_attr.?.arg] = proc_type.ret^
                } else if proc_type.ret_attr.?.type == .Out_Loc {
                    used_out_locations[proc_type.ret_attr.?.arg] = proc_type.ret^
                }
            }
        }
    }
    else
    {
        proc_type.ret = new(Ast_Type)
        proc_type.ret.kind = .None
    }

    required_token(p, .LBrace)
    if tokens[at].type != .RBrace {
        proc_def.statements = parse_statement_list(p)
    }
    required_token(p, .RBrace)
    return proc_def
}

parse_statement_list :: proc(using p: ^Parser) -> []^Ast_Statement
{
    scratch, _ := acquire_scratch()
    tmp_list := make([dynamic]^Ast_Statement, allocator = scratch)
    for true
    {
        stmt := parse_statement(p)
        if stmt != nil do append(&tmp_list, stmt)
        if tokens[at].type == .RBrace || tokens[at].type == .EOS do break
        if error do break
    }
    return slice.clone(tmp_list[:])
}

parse_statement :: proc(using p: ^Parser) -> ^Ast_Statement
{
    for optional_token(p, .Semi) {}

    node: ^Ast_Statement
    if optional_token(p, .If)
    {
        if_stmt := make_statement(p, Ast_If)
        if_stmt.cond = parse_expr(p)

        {
            old_scope := scope
            scope = new(Ast_Scope)
            scope.enclosing_scope = old_scope
            defer scope = old_scope

            if_stmt.scope = scope
            required_token(p, .LBrace)
            if tokens[at].type != .RBrace {
                if_stmt.statements = parse_statement_list(p)
            }
            required_token(p, .RBrace)
        }

        if optional_token(p, .Else)
        {
            if_stmt.else_is_present = true
            old_scope := scope
            if_stmt.else_scope = new(Ast_Scope)
            if_stmt.else_scope.enclosing_scope = old_scope
            defer scope = old_scope

            if tokens[at].type == .If
            {
                if_stmt.else_is_single = true
                if_stmt.else_single = parse_statement(p)
            }
            else
            {
                required_token(p, .LBrace)
                if tokens[at].type != .RBrace {
                    if_stmt.else_multi_statements = parse_statement_list(p)
                }
                required_token(p, .RBrace)
            }
        }

        node = if_stmt
    }
    else if optional_token(p, .For)
    {
        old_scope := scope
        scope = new(Ast_Scope)
        scope.enclosing_scope = old_scope
        defer scope = old_scope

        for_stmt := make_statement(p, Ast_For)
        for_stmt.scope = scope

        if tokens[at].type != .LBrace
        {
            if tokens[at].type != .Semi {
                for_stmt.define = parse_var_def(p)
            }
            required_token(p, .Semi)
            if tokens[at].type != .Semi {
                for_stmt.cond = parse_expr(p)
            }
            required_token(p, .Semi)
            if tokens[at].type != .LBrace {
                for_stmt.iter = parse_assign(p)
            }
        }

        required_token(p, .LBrace)

        if tokens[at].type != .RBrace {
            for_stmt.statements = parse_statement_list(p)
        }
        required_token(p, .RBrace)

        node = for_stmt
    }
    else if tokens[at].type == .Continue
    {
        stmt := make_statement(p, Ast_Continue)
        node = stmt
        at += 1
        required_token(p, .Semi)
    }
    else if tokens[at].type == .Break
    {
        stmt := make_statement(p, Ast_Break)
        node = stmt
        at += 1
        required_token(p, .Semi)
    }
    else if tokens[at].type == .Ident && tokens[at+1].type == .Colon
    {
        ident := tokens[at].text
        at += 2

        decl := make_node(p, Ast_Decl)
        decl.name = ident
        append(&scope.decls, decl)

        type: ^Ast_Type
        if tokens[at].type == .Assign
        {
            type = new(Ast_Type)
            type.kind = .Unknown
        }
        else
        {
            type = parse_type(p)
        }
        decl.type = type

        if optional_token(p, .Assign)
        {
            def_var := make_statement(p, Ast_Define_Var)
            def_var.decl = decl
            def_var.expr = parse_expr(p)
            node = def_var
        }

        required_token(p, .Semi)
    }
    else
    {
        cursor := at
        for ; tokens[cursor].type != .Semi && tokens[cursor].type != .Assign && tokens[cursor].type != .EOS; cursor += 1 { }

        found_assign := tokens[cursor].type == .Assign
        if found_assign
        {
            node = parse_assign(p)
        }
        else if optional_token(p, .Return)
        {
            ret_stmt := make_statement(p, Ast_Return)
            ret_stmt.expr = parse_expr(p)
            node = ret_stmt
        }
        else
        {
            stmt_expr := make_statement(p, Ast_Stmt_Expr)
            stmt_expr.expr = parse_expr(p)
            node = stmt_expr
        }

        required_token(p, .Semi)
    }

    return node
}

parse_assign :: proc(using p: ^Parser) -> ^Ast_Assign
{
    node := make_statement(p, Ast_Assign)
    node.lhs = parse_expr(p)
    required_token(p, .Assign)
    node.rhs = parse_expr(p)
    return node
}

parse_var_def :: proc(using p: ^Parser) -> ^Ast_Define_Var
{
    ident := required_token(p, .Ident).text
    required_token(p, .Colon)

    decl := make_node(p, Ast_Decl)
    decl.name = ident
    append(&scope.decls, decl)

    type: ^Ast_Type
    if tokens[at].type == .Assign
    {
        type = new(Ast_Type)
        type.kind = .Unknown
    }
    else
    {
        type = parse_type(p)
    }
    decl.type = type

    required_token(p, .Assign)

    def_var := make_statement(p, Ast_Define_Var)
    def_var.decl = decl
    def_var.expr = parse_expr(p)
    return def_var
}

parse_var_decl :: proc(using p: ^Parser) -> ^Ast_Decl
{
    node := make_node(p, Ast_Decl)
    append(&scope.decls, node)

    ident := required_token(p, .Ident)
    node.name = ident.text

    required_token(p, .Colon)
    node.type = parse_type(p)
    return node
}

parse_expr :: proc(using p: ^Parser, prec: int = max(int)) -> ^Ast_Expr
{
    lhs: ^Ast_Expr

    // Prefix operators
    prefix_expr: ^Ast_Expr
    base_expr := &prefix_expr
    for
    {
        prefix_op_info, is_prefix := Prefix_Ops[tokens[at].type]
        if !is_prefix do break

        // Typecast

        // Other prefix operators
        {
            tmp := make_expr(p, Ast_Unary_Expr)
            tmp.op = prefix_op_info.op
            base_expr^ = tmp
            base_expr = &tmp.expr
            p.at += 1
        }
    }

    // Postfix operators
    lhs = parse_postfix_expr(p)

    // Postfix ops have precedence over prefix ops
    if prefix_expr != nil
    {
        base_expr^ = lhs
        lhs = prefix_expr
    }

    // Binary operators
    for true
    {
        op, found := Op_Precedence[tokens[at].type]
        undo_recurse := false
        undo_recurse |= !found
        undo_recurse |= op.prec > prec  // If it's less important (=greater priority) don't recurse.
        undo_recurse |= op.prec == prec && op.is_left_to_right  // Handle left-to-right vs right-to-left operators
        if undo_recurse do return lhs

        // Recurse
        bin_op := make_expr(p, Ast_Binary_Expr)
        bin_op.op = op.op
        bin_op.lhs = lhs
        at += 1
        bin_op.rhs = parse_expr(p, op.prec)
        lhs = bin_op
    }

    return lhs
}

parse_primary_expr :: proc(using p: ^Parser) -> ^Ast_Expr
{
    expr: ^Ast_Expr
    if optional_token(p, .LParen)
    {
        internal := parse_expr(p)
        required_token(p, .RParen)
        expr = internal
    }
    else if tokens[at].type == .Ident
    {
        expr = make_expr(p, Ast_Ident_Expr)
        at += 1
    }
    else if tokens[at].type == .IntLit || tokens[at].type == .FloatLit
    {
        expr = make_expr(p, Ast_Lit_Expr)
        at += 1
    }
    else if tokens[at].type == .True || tokens[at].type == .False
    {
        expr = make_expr(p, Ast_Lit_Expr)
        at += 1
    }

    return expr
}

parse_postfix_expr :: proc(using p: ^Parser) -> ^Ast_Expr
{
    expr := parse_primary_expr(p)
    loop: for true
    {
        #partial switch tokens[at].type
        {
            case .Dot:
            {
                member_access := make_expr(p, Ast_Member_Access)
                at += 1

                ident := required_token(p, .Ident)
                member_access.member_name = ident.text
                member_access.target = expr
                expr = member_access
            }
            case .LParen:
            {
                call := make_expr(p, Ast_Call)
                call.target = expr
                at += 1

                if tokens[at].type != .RParen
                {
                    scratch, _ := acquire_scratch()
                    tmp_list := make([dynamic]^Ast_Expr, allocator = scratch)
                    for true
                    {
                        append(&tmp_list, parse_expr(p))
                        comma_present := optional_token(p, .Comma)
                        if !comma_present do break
                        if comma_present && (tokens[at].type == .RParen || tokens[at].type == .RBrace) do break
                        if error do break
                    }

                    call.args = slice.clone(tmp_list[:])
                }

                required_token(p, .RParen)

                expr = call
            }
            case .LBracket:
            {
                array_access := make_expr(p, Ast_Array_Access)
                at += 1

                array_access.idx_expr = parse_expr(p)
                array_access.target = expr
                expr = array_access

                required_token(p, .RBracket)
            }
            case: break loop
        }
    }

    return expr
}

parse_decl_list :: proc(using p: ^Parser, add_to_scope: bool) -> []^Ast_Decl
{
    scratch, _ := acquire_scratch()
    tmp_list := make([dynamic]^Ast_Decl, allocator = scratch)
    for true
    {
        if tokens[at].type == .RParen || tokens[at].type == .RBrace do break
        if error do break

        append(&tmp_list, parse_decl_list_elem(p, add_to_scope))
        comma_present := optional_token(p, .Comma)
        if !comma_present do break
    }
    return slice.clone(tmp_list[:])
}

parse_decl_list_elem :: proc(using p: ^Parser, add_to_scope: bool) -> ^Ast_Decl
{
    node := make_node(p, Ast_Decl)
    if add_to_scope {
        append(&scope.decls, node)
    }

    ident := required_token(p, .Ident)
    required_token(p, .Colon)

    node.name = ident.text

    node.type = parse_type(p)
    node.attr = parse_attribute(p)
    if node.attr != nil
    {
        if node.attr.?.type == .Data {
            used_data_type = node.type
        } else if node.attr.?.type == .Indirect_Data {
            used_indirect_data_type = node.type
        }

        if node.attr.?.type == .In_Loc {
            used_in_locations[node.attr.?.arg] = node.type^
        } else if node.attr.?.type == .Out_Loc {
            used_out_locations[node.attr.?.arg] = node.type^
        }
    }

    return node
}

parse_type :: proc(using p: ^Parser) -> ^Ast_Type
{
    base: ^Ast_Type
    node: ^Ast_Type

    for true
    {
        if optional_token(p, .LBracket)
        {
            slice_type := new(Ast_Type)
            slice_type.kind = .Slice
            if node != nil do node.base = slice_type
            node = slice_type
            if base == nil do base = node

            required_token(p, .RBracket)
        }
        else if optional_token(p, .Caret)
        {
            ptr_type := new(Ast_Type)
            ptr_type.kind = .Pointer
            if node != nil do node.base = ptr_type
            node = ptr_type
            if base == nil do base = node
        }
        else do break
    }

    ident := required_token(p, .Ident)
    prim_type: Ast_Type_Primitive_Kind
    switch ident.text
    {
        case "float": prim_type = .Float
        case "uint": prim_type = .Uint
        case "int": prim_type = .Int
        case "vec2": prim_type = .Vec2
        case "vec3": prim_type = .Vec3
        case "vec4": prim_type = .Vec4
        case "bool": prim_type = .Bool
        case "textureid": prim_type = .Texture_ID
        case "samplerid": prim_type = .Sampler_ID
        case "mat4": prim_type = .Mat4
        case "Ray_Query": prim_type = .Ray_Query
        case "bvh_id": prim_type = .BVH_ID
        case: prim_type = .None
    }

    ident_node := new(Ast_Type)
    ident_node.name = ident
    ident_node.primitive_kind = prim_type
    if node != nil do node.base = ident_node
    node = ident_node
    if base == nil do base = node

    if prim_type == .None {
        node.kind = .Label
    } else {
        node.kind = .Primitive
    }

    add_type_if_not_present(p, base)
    return base
}

parse_attribute :: proc(using p: ^Parser) -> Maybe(Ast_Attribute)
{
    if tokens[at].type != .Attribute do return nil

    attr := Ast_Attribute {}

    token := required_token(p, .Attribute)

    switch token.text
    {
        case "vert_id":              attr.type = .Vert_ID
        case "position":             attr.type = .Position
        case "data":                 attr.type = .Data
        case "instance_id":          attr.type = .Instance_ID
        case "draw_id":              attr.type = .Draw_ID
        case "indirect_data":        attr.type = .Indirect_Data
        case "workgroup_id":         attr.type = .Workgroup_ID
        case "local_invocation_id":  attr.type = .Local_Invocation_ID
        case "group_size":           attr.type = .Group_Size
        case "global_invocation_id": attr.type = .Global_Invocation_ID
        case "in_loc":
        {
            // ??? Why is the compiler making me do this?
            attr.type, _ = .In_Loc,
            required_token(p, .LParen)
            num_token := required_token(p, .IntLit)
            attr.arg = u32(get_token_lit_int_value(num_token))
            required_token(p, .RParen)
        }
        case "out_loc":
        {
            // ??? Why is the compiler making me do this?
            attr.type, _ = .Out_Loc,
            required_token(p, .LParen)
            num_token := required_token(p, .IntLit)
            attr.arg = u32(get_token_lit_int_value(num_token))
            required_token(p, .RParen)
        }
        case:
        {
            parse_error(p, "Unknown attribute '%v'.", token.text)
        }
    }

    return attr
}

make_node :: proc(using p: ^Parser, $T: typeid) -> ^T
{
    node := new(T)
    node.token = tokens[at]
    return node
}

make_expr :: proc(using p: ^Parser, $T: typeid) -> ^T
{
    node := new(T)
    node.token = tokens[at]
    node.derived_expr = node
    return node
}

make_statement :: proc(using p: ^Parser, $T: typeid) -> ^T
{
    node := new(T)
    node.token = tokens[at]
    node.derived_statement = node
    return node
}

parse_error :: proc(using p: ^Parser, fmt_str: string, args: ..any)
{
    parse_error_on_token(p, tokens[at], fmt_str, ..args)
}

parse_error_on_token :: proc(using p: ^Parser, token: Token, fmt_str: string, args: ..any)
{
    if error do return

    error_msg(filename, token, fmt_str, ..args)
    error = true
}

required_token :: proc(using p: ^Parser, type: Token_Type) -> Token
{
    if tokens[at].type != type
    {
        parse_error(p, "Unexpected token '%v': expecting '%v'", tokens[at].text, token_type_to_string(type))
        return {}
    }

    at += 1
    return tokens[at-1]
}

optional_token :: proc(using p: ^Parser, type: Token_Type) -> bool
{
    if tokens[at].type == type
    {
        at += 1
        return true
    }

    return false
}

// Operator precedence
Op_Info :: struct #all_or_none
{
    prec: int,
    op: Ast_Binary_Op,
    is_left_to_right: bool,
}
Op_Precedence := map[Token_Type]Op_Info {
    .Mul = { 3, .Mul, true },
    .Div = { 3, .Div, true },

    .Plus  = { 4, .Add, true },
    .Minus = { 4, .Minus, true },

    .LShift = { 5, .LShift, true },
    .RShift = { 5, .RShift, true },

    .Greater = { 6, .Greater, true },
    .Less    = { 6, .Less, true },
    .GE      = { 6, .GE, true },
    .LE      = { 6, .LE, true },

    .EQ      = { 7, .EQ, true },
    .NEQ     = { 7, .NEQ, true },

    .Bitwise_And = { 8, .Bitwise_And, true },
    .Bitwise_Xor = { 9, .Bitwise_Xor, true },
    .Bitwise_Or  = { 10, .Bitwise_Or, true },

    .And = { 11, .And, true },
    .Or  = { 12, .Or, true },
}
Unary_Op_Info :: struct #all_or_none
{
    op: Ast_Unary_Op,
}
Prefix_Ops := map[Token_Type]Unary_Op_Info {
    .Not = { .Not },
    .Minus = { .Minus },
    .Plus = { .Plus },
}

add_type_if_not_present :: proc(using p: ^Parser, type: ^Ast_Type)
{
    if type == nil do return

    for &used in used_types {
        if same_type(type, &used) do return
    }

    append(&used_types, type^)
}

type_to_string :: proc(type: ^Ast_Type, arena: runtime.Allocator) -> string
{
    scratch, _ := acquire_scratch(arena)
    if type == nil do return "NIL"

    res: string
    switch type.kind
    {
        case .Poison:    res = "POISON"
        case .None:      res = "none"
        case .Unknown:   res = "UNKNOWN"
        case .Label:     res = type.name.text
        case .Pointer:   res = str.concatenate({ "^", type_to_string(type.base, scratch) }, allocator = scratch)
        case .Slice:     res = str.concatenate({ "[]", type_to_string(type.base, scratch) }, allocator = scratch)
        case .Proc:
        {
            scratch2, _ := acquire_scratch(scratch, arena)
            sb := str.builder_make_none(allocator = scratch2)
            str.write_string(&sb, "(")
            for arg, i in type.args
            {
                str.write_string(&sb, type_to_string(arg.type, arena = scratch))
                if i < len(type.args) - 1 do str.write_string(&sb, ", ")
            }
            str.write_string(&sb, ")")
            if type.ret == nil
            {
                str.write_string(&sb, " -> ")
                str.write_string(&sb, type_to_string(type.ret, arena = scratch))
            }

            res = str.clone(str.to_string(sb), allocator = scratch)
        }
        case .Primitive:  res = type.name.text
        case .Struct:
        {
            scratch2, _ := acquire_scratch(scratch, arena)
            sb := str.builder_make_none(allocator = scratch2)
            str.write_string(&sb, "(")
            for member, i in type.members
            {
                str.write_string(&sb, type_to_string(member.type, arena = scratch))
                if i < len(type.members) - 1 do str.write_string(&sb, ", ")
            }
            str.write_string(&sb, ")")
            if type.ret == nil
            {
                str.write_string(&sb, " -> ")
                str.write_string(&sb, type_to_string(type.ret, arena = scratch))
            }

            res = str.clone(str.to_string(sb), allocator = scratch)
        }
    }

    return str.clone(res, allocator = arena)
}
